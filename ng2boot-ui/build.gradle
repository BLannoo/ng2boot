// The Docker build plugin needs an assemble task
task assemble(type: Copy) {
    from 'src/dist'
    into 'build/docker/libs'
}

apply plugin: 'com.moowork.node'
apply plugin: 'com.cegeka.gradle.docker-build'
apply plugin: 'com.cegeka.gradle.docker-nexus'

dockerBuild {
    registry = "$project.group"
}

dockerNexus {
    nexusHost = "$project.ext.nexusHost"
    nexusPort = "$project.ext.nexusPort"
}

idea {
    module {
        excludeDirs += file('node_modules')
        downloadSources = true
    }
}

node {
    version = '7.2.0'
    download = true
    distBaseUrl = 'https://nodejs.org/dist'
    workDir = file("$project.buildDir/nodejs")
    nodeModulesDir = file("$project.projectDir")
}

npmInstall {
    workingDir = file('./src')
    outputs.upToDateWhen {false}
}

task ngTest(type: NpmTask) {
    dependsOn npmInstall
    workingDir = file('./src')
    args = ['run', 'ngSingleTest']
}

task ngBuildProd(type: NpmTask) {
    dependsOn npmInstall
    workingDir = file('./src')
    args = ['run', 'buildProd']
}

assemble.dependsOn 'ngBuildProd'
