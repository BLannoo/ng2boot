buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath 'com.cegeka.gradle:docker-build:1.0'
		classpath 'com.cegeka.gradle:docker-nexus:1.0'
		classpath 'com.cegeka.gradle:docker-compose:1.0'
		classpath 'com.cegeka.gradle:manage-version:1.0'
	}
}

plugins {
    id 'io.spring.dependency-management' version '1.0.2.RELEASE'
}

apply plugin: 'com.cegeka.gradle.manage-version'
apply plugin: 'com.cegeka.gradle.docker-compose'

project.ext {
    javaProjects = subprojects.findAll {
        it.name == 'ng2boot-rest'
    }
}

subprojects {
    apply plugin: 'idea'

    ext {
        nexusHost = "$nexusHost"
        nexusPort = "$nexusPort"
    }

    group applicationGroup
    version applicationVersion
}

configure(project.javaProjects) {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext {
        springBootVersion = '1.5.3.RELEASE'
        jerseyVersion = '2.23.2'
        hibernateJava8Version = '5.0.0.Final'
        jacksonVersion = '2.6.1'
        postgresqlVersion = '9.4-1206-jdbc42'
        h2Version = '1.3.148'
        junitVersion = '4.12'
        assertjVersion = '3.2.0'
        mockitoVersion = '2.0.31-beta'
    }

    dependencyManagement {
        dependencies {
            dependency "org.glassfish.jersey.core:jersey-client:$jerseyVersion"
            dependency "org.glassfish.jersey.ext:jersey-proxy-client:$jerseyVersion"

            dependency "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-jersey:$springBootVersion"
            dependency "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"

            dependency "org.hibernate:hibernate-java8:$hibernateJava8Version"
            dependency "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"

            dependency "com.h2database:h2:$h2Version"
            dependency "org.postgresql:postgresql:$postgresqlVersion"

            dependency "junit:junit:$junitVersion"
            dependency "org.assertj:assertj-core:$assertjVersion"
            dependency "org.mockito:mockito-core:$mockitoVersion"
        }
    }
}